import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript{
  ext {
    kotlinVersion = '1.4.30'
  }
}

plugins {
  id("org.jetbrains.kotlin.jvm") version "$kotlinVersion"
  id('java-library')
  id("org.jetbrains.dokka") version "0.10.0"
  id('maven-publish')
  id("com.diffplug.spotless") version "5.9.0"
  id('jacoco')
  id('idea')
}

apply from: 'jacoco.gradle'

group = "tech.relaycorp"

repositories {
  jcenter()
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

  api 'tech.relaycorp:cogrpc:[1.1.6,2.0.0)'
  api "io.grpc:grpc-okhttp:[1.32.2,2.0.0)"

  // Testing
  testImplementation "org.junit.jupiter:junit-jupiter:5.7.1"
  testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
  testImplementation "org.mockito:mockito-inline:3.7.7"
}

java {
  withJavadocJar()
  withSourcesJar()
}

tasks.withType(KotlinCompile).configureEach {
  kotlinOptions.jvmTarget = "1.8"
}

tasks.withType(KotlinCompile).all {
  kotlinOptions.freeCompilerArgs = kotlinOptions.freeCompilerArgs + [
    "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
    "-Xuse-experimental=kotlinx.coroutines.FlowPreview",
    "-Xuse-experimental=kotlin.time.ExperimentalTime"
  ]
}

// Documentation

tasks.dokka {
  outputFormat = "html"
  outputDirectory = "$buildDir/javadoc"
}

// Publishing

publishing {
  publications {
    create("default", MavenPublication) {
      from(components["java"])

      pom {
        name.set(rootProject.name)
        description.set("OkHTTP channel for Relaynet CogRPC binding")
        url.set("https://github.com/relaycorp/relaynet-cogrpc-jvm-okhttp")
        developers {
          developer {
            id.set("relaycorp")
            name.set("Relaycorp, Inc.")
            email.set("no-reply@relaycorp.tech")
          }
        }
        licenses {
          license {
            name.set("Apache-2.0")
          }
        }
        scm {
          connection.set("scm:git:https://github.com/relaycorp/relaynet-cogrpc-jvm-okhttp.git")
          developerConnection.set(
            "scm:git:https://github.com/relaycorp/relaynet-cogrpc-jvm-okhttp.git"
          )
          url.set("https://github.com/relaycorp/relaynet-cogrpc-jvm-okhttp")
        }
      }
    }
  }
  repositories {
    maven {
      // publish=1 automatically publishes the version
      url = uri(
        "https://api.bintray.com/maven/relaycorp/maven/tech.relaycorp.cogrpc.okhttp/;publish=1"
      )
      credentials {
        username = System.getenv("BINTRAY_USERNAME")
        password = System.getenv("BINTRAY_KEY")
      }
    }
  }
}

// Lint

spotless {
  def ktlintUserData = [
    "max_line_length": "100",
    "disabled_rules": "import-ordering"
  ]
  kotlin {
    ktlint("0.36.0").userData(ktlintUserData)
  }
  kotlinGradle {
    ktlint().userData(ktlintUserData)
  }
}
